group "com.github.zutherb.gradle"

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task compileJavaAppLuncher(type: Exec) {
    def javaAppLuncherDir = "${buildDir}/resources/main/com/github/zutherb/gradle/mapAppBundle"
    mkdir javaAppLuncherDir
    executable  "gcc"
    args        "-I", "/Library/Java/JavaVirtualMachines/jdk1.7.0_45.jdk/Contents/Home/include",
                "-I", "/Library/Java/JavaVirtualMachines/jdk1.7.0_45.jdk/Contents/Home/include/darwin",
                "-o", "${javaAppLuncherDir}/JavaAppLauncher",
                "-framework", "Cocoa",
                "-arch", "x86_64",
                "-isysroot", "/Applications/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.9.sdk",
                "-mmacosx-version-min=10.7",
                "${projectDir}/src/main/object-c/main.m",
                "-v"
}

tasks.processResources.dependsOn(compileJavaAppLuncher)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Gradle MacAppBundle Plugin",
                "Implementation-Version": version,
                "Author": "Bernd Zuther <bernd.zuther@me.com>",
        )
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name        'Gradle MacAppBundle Plugin'
                packaging   'jar'
                description 'The gradle-macappbundle plugin creates Mac OSX .app applications from within Gradle. ' +
                            'The runSetFile and createDmg tasks will probably only run on under OSX as they use the ' +
                            'SetFile and hdiutil applications.'
                url         'https://github.com/zutherb/gradle-macosx-java-application-bundle-plugin'

                scm {
                    url                 'scm:git@github.com:zutherb/gradle-macosx-java-application-bundle-plugin.git'
                    connection          'scm:git@github.com:zutherb/gradle-macosx-java-application-bundle-plugin.git'
                    developerConnection 'scm:git@github.com:zutherb/gradle-macosx-java-application-bundle-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'zutherb'
                        name 'Bernd Zuther'
                    }
                }
            }
        }
    }
}